# --------------------------------------
# PKGBUILD for sumneko's lua LSP
# --------------------------------------
pkgname=lua-language-server

pkgdesc="Lua Language Server coded by Lua"
url="https://github.com/sumneko/lua-language-server"

pkgver=1.10.0
pkgrel=1

arch=('x86_64')
license=('MIT')

conflicts=("${pkgname}-git")

depends=('lua')
makedepends=('ninja')

source=("git+https://github.com/sumneko/${pkgname}.git"
        "change_file_path_locations.patch"
        "remove_telemetry.patch")

sha512sums=('SKIP'
            '6ed409397641c27ac24b1b3cfab098d264b9b7e8930693a37befb497b22b04dce8998fe69add944b358bae6a4baff9ca93d617fc7bfc05e4ca3ecb3b3162bf47'
            '5a403d1484c1a2212066938dc9660c69b5f13eb0827968ebcd7ede15b9045bf26530b4b7e028d4826cc29d4f8b82206a58eea426d04cda4bab72c6154b394ea4')

prepare() {
    cd "${srcdir}/${pkgname}"

    git checkout ${pkgver}
    git submodule update --init --recursive

    patch -p1 -i "${srcdir}/change_file_path_locations.patch"
    patch -p1 -i "${srcdir}/remove_telemetry.patch"
}

build() {
    cd "${srcdir}/${pkgname}"

    ninja -C 3rd/luamake -f ninja/linux.ninja
    ./3rd/luamake/luamake rebuild
}

package() {
    cd "${srcdir}/${pkgname}"

    install -m0755 -d "${pkgdir}/usr/lib/${pkgname}"
    cp -a bin/Linux/* "${pkgdir}/usr/lib/${pkgname}"

    install -m0755 -d "${pkgdir}/usr/share/${pkgname}"

    cp -a main.lua platform.lua debugger.lua locale script meta \
        "${pkgdir}/usr/share/${pkgname}"
}
