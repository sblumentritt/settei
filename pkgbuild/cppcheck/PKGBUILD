# --------------------------------------
# improved PKGBUILD for cppcheck
# --------------------------------------
pkgname_=cppcheck
pkgname=cppcheck-cli

pkgdesc="A tool for static C/C++ code analysis"
url="https://github.com/danmar/cppcheck"

pkgver=1.90
pkgrel=1

arch=('x86_64')
license=('GPL')

provides=("${pkgname_}")
conflicts=("${pkgname_}")

makedepends=('clang' 'cmake')

source=("${pkgname_}.tar.gz::https://github.com/danmar/${pkgname_}/archive/${pkgver}.tar.gz")
sha512sums=('7e6804952c2243533c54e9a57fb70f11188a4664ced44d791be253780d72a1c71f856874036933682fc964a1283cf69ced5e91f49d9cf0ceb4302ac4f45ee07f')

build()
{
    cmake -S ${pkgname_}-${pkgver} -B ${pkgname_}-${pkgver}/build \
          -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_C_COMPILER=clang \
          -DCMAKE_CXX_COMPILER=clang++ \
          -DUSE_CLANG=ON \
          -DHAVE_RULES=ON

    cmake --build ${pkgname_}-${pkgver}/build
}

package()
{
    DESTDIR="${pkgdir}" \
        CFGDIR=/usr/share/Cppcheck \
        HAVE_RULES=yes \
        cmake --install ${pkgname_}-${pkgver}/build
}
