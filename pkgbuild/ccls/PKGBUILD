# --------------------------------------
# improved PKGBUILD for ccls
# --------------------------------------
pkgname=ccls-git
pkgname_=ccls

pkgdesc="C/C++ language server"
url="https://github.com/MaskRay/ccls"

pkgver=r1895.41e7d6a7
pkgrel=1

arch=('x86_64')
license=('Apache')

provides=("${pkgname}")

depends=('clang')
makedepends=('git' 'cmake' 'llvm')

source=("git+https://github.com/MaskRay/${pkgname_}.git")
sha512sums=('SKIP')

pkgver()
{
    cd "${srcdir}/${pkgname_}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare()
{
    # get submodules
    cd "${srcdir}/${pkgname_}"
    git submodule update --init
}

build()
{
    cmake -S ${pkgname_} -B ${pkgname_}/build \
          -DCMAKE_CXX_COMPILER=clang++ \
          -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr

    cmake --build ${pkgname_}/build
}

package()
{
    DESTDIR="${pkgdir}" cmake --install ${pkgname_}/build

    install -m644 -D "${srcdir}/${pkgname_}/LICENSE" \
        "${pkgdir}/usr/share/licenses/${pkgname_}/LICENSE"
}
