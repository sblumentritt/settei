# --------------------------------------
# PKGBUILD for neovim
# --------------------------------------
pkgname=neovim-git
pkgname_=neovim

pkgdesc="Vim-fork focused on extensibility and usability"
url="https://github.com/neovim/neovim"

pkgver=r16648.ce70edb80
pkgrel=1

arch=('x86_64')
license=('custom:neovim')

provides=("${pkgname_}")
conflicts=("${pkgname_}" "${pkgname_}-nightly")

optdepends=('python-pynvim' 'wl-clipboard' 'xclip' 'xsel')
makedepends=('clang' 'cmake' 'ninja' 'lua51-mpack' 'lua51-lpeg' 'gperf')
depends=('libtermkey' 'libuv' 'libluv' 'libutf8proc' 'msgpack-c' 'unibilium' 'libvterm' 'luajit' 'tree-sitter')

source=("git+https://github.com/${pkgname_}/${pkgname_}.git")
sha256sums=('SKIP')

pkgver()
{
    cd "${srcdir}/${pkgname_}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build()
{
    cmake -S ${pkgname_} -B ${pkgname_}/build \
          -GNinja \
          -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr

    cmake --build ${pkgname_}/build
}

check()
{
  "${pkgname_}/build/bin/nvim" --version
  "${pkgname_}/build/bin/nvim" --headless -u NONE -i NONE -c ':quit'
}

package()
{
    DESTDIR="${pkgdir}" cmake \
        --build ${pkgname_}/build \
        --target install

    install -m644 -D "${srcdir}/${pkgname_}/LICENSE" \
    	"${pkgdir}/usr/share/licenses/${pkgname_}/LICENSE"

    # required for vim packages install via Arch
    mkdir -p "${pkgdir}"/usr/share/vim
    printf "set runtimepath+=/usr/share/vim/vimfiles\n" > "${pkgdir}"/usr/share/nvim/sysinit.vim
}
