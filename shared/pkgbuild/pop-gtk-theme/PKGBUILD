# --------------------------------------
# PKGBUILD for Pop OS gtk theme
# --------------------------------------
pkgname=pop-gtk-theme
pkgdesc="A GTK+ theme for Pop!_OS"
url="https://github.com/pop-os/gtk-theme"

pkgver=5.3.1
pkgrel=1

arch=('x86_64')
license=('GPL2' 'CCPL')

depends=('gtk3' 'gtk2' 'gtk-engine-murrine' 'gdk-pixbuf2')
makedepends=('meson' 'ninja' 'glib2' 'sassc')

source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha512sums=('8ac9258bfb9c7985ac3955b0f1102ba792ec24c3628f832154ef5958bb9d8c10e4f5c45a7beb9236391d3202d0b3972406d65c5dcd8852736d6e7606e283a015')

# configure options to decrease build time
options=('!strip' '!zipman' 'libtool' 'staticlibs')

build() {
    cd "${srcdir}/gtk-theme-${pkgver}"
    meson --prefix='/usr' build
    ninja -C build
}

package() {
    cd "${srcdir}/gtk-theme-${pkgver}"
    DESTDIR="${pkgdir}" ninja -C build install
}
