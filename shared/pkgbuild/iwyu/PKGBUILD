# --------------------------------------
# improved PKGBUILD for iwyu
# --------------------------------------
pkgname=iwyu
pkgname_long=include-what-you-use

pkgdesc="A tool for use with clang to analyze #includes in C and C++ source files"
url="https://include-what-you-use.org"

pkgver=0.16
pkgrel=1

arch=('x86_64')
license=('custom:LLVM')

provides=("${pkgname_long}")
conflicts=("${pkgname_long}" "${pkgname_long}-git")

depends=('clang' 'python')
makedepends=('clang' 'llvm')

source=("https://github.com/${pkgname_long}/${pkgname_long}/archive/${pkgver}.tar.gz")
sha512sums=('2923e18c40ddc5c03458b896d991adde03d489c6645ecc3cc4f69a508e323534afdef3233943e99533ffc21c64a10a8a5c635dc4797a61edf09d0a7a918a3cd5')

build() {
    cmake -S ${pkgname_long}-${pkgver} -B ${pkgname_long}-${pkgver}/build \
          -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX:PATH=/usr \
          -DCMAKE_PREFIX_PATH=/usr/lib \
          -DCMAKE_C_COMPILER=clang \
          -DCMAKE_CXX_COMPILER=clang++ \
          -Wno-dev

    cmake --build ${pkgname_long}-${pkgver}/build
}

package() {
    DESTDIR="${pkgdir}" cmake --install ${pkgname_long}-${pkgver}/build

    install -D -m644 ${pkgname_long}-${pkgver}/LICENSE.TXT \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    install -D -m755 ${pkgname_long}-${pkgver}/fix_includes.py \
        "${pkgdir}/usr/bin/iwyu-fix-includes"

    install -D -m755 ${pkgname_long}-${pkgver}/iwyu_tool.py \
        "${pkgdir}/usr/bin/iwyu-tool"

    # remove files which are installed under a different name
    rm -f "${pkgdir}/usr/bin/fix_includes.py"
    rm -f "${pkgdir}/usr/bin/iwyu_tool.py"
}
